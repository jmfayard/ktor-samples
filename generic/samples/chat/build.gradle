buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:_"
    }
}

apply plugin: 'kotlin-multiplatform'

kotlin {
    targets {
        js('frontend') {
            browser {
                testTask {
                    enabled = false
                }

                dceTask {
                    keep 'ktor-ktor-io.\$\$importsForInline\$\$.ktor-ktor-io.io.ktor.utils.io'
                }

                distribution {
                    directory = file("$projectDir/src/backendMain/resources/web")
                }
            }
        }
        jvm('backend') {
            compilations.main {
                tasks.getByName(processResourcesTaskName) {
                    dependsOn(frontendBrowserWebpack)
                }
            }
        }
    }

    sourceSets {
        backendMain {
            dependencies {
                implementation Kotlin.stdlib.jdk8
                implementation Ktor.server.netty
                implementation "io.ktor:ktor-websockets:_"
                implementation "ch.qos.logback:logback-classic:_"
            }
        }

        backendTest {
            dependencies {
                implementation Ktor.server.testHost
            }
        }

        frontendMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
                implementation Ktor.client.websockets
                implementation "io.ktor:ktor-client-js:_"
                implementation KotlinX.coroutines.coreJs
            }
        }
    }
}

repositories {
    mavenCentral()
}

task run(type: JavaExec, dependsOn: [backendJar]) {
    main = "io.ktor.samples.chat.backend.ChatApplicationKt"
    classpath(configurations.backendRuntimeClasspath, backendJar)
    args = []
}
